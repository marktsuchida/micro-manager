buildscript {
   repositories {
      jcenter()
   }
   dependencies {
      classpath 'com.cookpad.android.licensetools:license-tools-plugin:1.2.0'
   }
}

allprojects {
   apply plugin:'maven'
   apply plugin:'maven-publish'
   apply plugin:'com.cookpad.android.licensetools'

   group = 'org.micromanager'

   repositories {
      jcenter()
      mavenCentral()
      maven { url 'https://maven.imagej.net/content/groups/public' }

      // Jar committed to GitHub (for Bulenkov IconLoader)
      ivy {
         url 'https://github.com/'
         layout 'pattern', {
            artifact '[organisation]/[module]/raw/[revision]/[module].[ext]'
         }
      }
   }

   licenseTools {
      ignoredGroups = ['org.micromanager', 'bulenkov']
   }

   // Enabling will block build, since many of our dependencies don't declare a
   // license in their pom.xml.
   checkLicenses.enabled = false

   publishing {
      repositories {
         mavenLocal()
      }
   }

   // Allow passing javac options on command line via -Pjavacflags=...
   if (project.hasProperty('javacflags')) {
      gradle.projectsEvaluated {
         tasks.withType(JavaCompile) {
            for (flag in project.property('javacflags').split()) {
               options.compilerArgs << flag
            }
         }
      }
   }
}

ext {
   stageDir = project.file(project.hasProperty('stageDir') ? project.property('stageDir') : 'stage')
}
